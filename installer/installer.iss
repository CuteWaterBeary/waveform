; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Waveform"
#define MyAppVersion "1.2.3-beta1"
#define MyAppURL "https://github.com/phandasm/waveform"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{57803E04-CC84-4877-AB1F-059130896D69}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={reg:HKLM\Software\OBS Studio,|{commonpf32}\obs-studio}
AppendDefaultDirName=no
DirExistsWarning=no
DefaultGroupName={#MyAppName}
LicenseFile={#SourcePath}\..\LICENSE
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputBaseFilename=Waveform_setup
Compression=lzma
SolidCompression=yes
WizardStyle=modern
Uninstallable=WizardIsTaskSelected('create_uninstaller')

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: create_uninstaller; Description: "Create Uninstaller"

[Files]
Source: "{#SourcePath}\bin\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Code]
function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Result := False;
  if (CurPageID = wpSelectDir) then begin
    if DirExists(ExpandConstant('{app}\obs-plugins\64bit')) then
      Result := True;
    if DirExists(ExpandConstant('{app}\obs-plugins\32bit')) then
      Result := True;
    if not Result then
      MsgBox('Could not locate obs-plugins folders. Please select the OBS root install directory.', mbError, MB_OK);
  end
  else
  begin
    Result := True;
  end;
end; 

